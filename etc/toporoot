#!/bin/tcsh -f
#
# Change all Topographica CVS/Root entries to list the new CVS host
# See Usage below.

set filename='Root'
set accesstype=':ext:'
set user=${USER}
set host=topographica.cvs.sourceforge.net
set rpath="/cvsroot/topographica"
set defaultdirectories="."
set TMP_FILE=/tmp/Root$$

# Parse arguments
while (opt${1:q} =~ opt-*)
    switch (opt${1:q})
    case opt-anonymous:
        set accesstype = ":pserver:"
        set user = "anonymous"
        breaksw
    case opt-user:
        set user = "$argv[2]"
	shift
        breaksw
    default:
        echo 'Usage:  toporoot [-anonymous | -user <sf-username>]'
        echo ' '
        echo 'Use -anonymous for read-only access.  If your local username does'
        echo 'not match your sourceforge.net username, you will need to specify'
        echo 'your <sf-username> with -user.'
        exit 2
        breaksw
    endsw
    shift
end


# Update files
echo "${accesstype}${user}@${host}:${rpath}" > ${TMP_FILE}
echo "New CVS/${filename}:" `cat ${TMP_FILE}`

foreach file (`find ${defaultdirectories} -name "${filename}" | & grep CVS/${filename}'$'`)
   echo "Updating $file"
   cp -f ${TMP_FILE} ${file}
end

rm -f ${TMP_FILE}
#
